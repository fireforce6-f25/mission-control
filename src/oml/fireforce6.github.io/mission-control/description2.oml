@dc:title "Mission Control extended description (states & scenarios)"
@dc:description "State machines and scenario instances that extend description1 for the Mission Control Dashboard ConOps."
description <https://fireforce6.github.io/mission-control/description2#> as description2
{
    uses <http://purl.org/dc/elements/1.1/> as dc
    uses <https://fireforce6.github.io/sierra/base#> as base
    uses <https://fireforce6.github.io/sierra/state#> as state
    uses <https://fireforce6.github.io/sierra/scenario#> as scenario
    extends <https://fireforce6.github.io/mission-control/description1#> as description1


    instance D_alert : base:Data [
        base:description "Alert signal (critical notification) produced by FireCloud or AIFireWarden."
    ]

    instance D_ai_proposal : base:Data [
        base:description "Structured task proposal from AI Fire Warden (may require approval)."
    ]

    instance D_approval : base:Data [
        base:description "Operator approval decision (approve / reject) for AI action."
    ]

    instance D_history_request : base:Data [
        base:description "Request for historical telemetry/time-series data."
    ]

    instance D_history_response : base:Data [
        base:description "Historical telemetry/time-series data returned from Fire Cloud."
    ]


    instance MissionControlDashboardStateMachine : state:StateMachine [
        base:description "State machine modeling the Mission Control Dashboard operational modes."
        state:isAllocatedTo description1:DashboardSystem
        base:contains Idle
        base:contains Monitoring
        base:contains AlertHandling
        base:contains DecisionPending
        base:contains HistoricalReview
        base:contains DashboardInitial
        base:contains DashboardFinal
    ]

    instance DashboardInitial : state:Initial [
        base:description "Initial dashboard state (startup / not connected)."
    ]

    instance Idle : state:Mode [
        base:description "Dashboard is idle or on standby; not actively streaming."
    ]

    instance Monitoring : state:Mode [
        base:description "Dashboard is actively monitoring live data and updating the map."
    ]

    instance AlertHandling : state:Mode [
        base:description "Dashboard is handling a critical alert; displaying pop-up and workflow."
    ]

    instance DecisionPending : state:Mode [
        base:description "Dashboard is awaiting human approval for an AI-suggested task."
    ]

    instance HistoricalReview : state:Mode [
        base:description "Dashboard is in historical/time-travel view mode (slider engaged)."
    ]

    instance DashboardFinal : state:Final [
        base:description "Dashboard session ended / system shutdown."
    ]

    relation instance T_startMonitoring : state:Transition [
        from DashboardInitial
        to Monitoring
        base:description "Startup / user starts monitoring."
        state:isTriggeredBy D_history_response
    ]

    relation instance T_idle_to_monitoring : state:Transition [
        from Idle
        to Monitoring
        base:description "Operator starts live monitoring."
        state:isTriggeredBy D_alert
    ]

    relation instance T_monitoring_receive_alert : state:Transition [
        from Monitoring
        to AlertHandling
        base:description "Incoming critical alert moves dashboard into AlertHandling state."
        state:isTriggeredBy D_alert
    ]

    relation instance T_alert_acknowledge : state:Transition [
        from AlertHandling
        to Monitoring
        base:description "Operator acknowledges alert; if no approval required, return to Monitoring."
        state:isTriggeredBy D_approval
    ]

    relation instance T_alert_requires_approval : state:Transition [
        from AlertHandling
        to DecisionPending
        base:description "Alert triggers an AI proposal that requires human approval; go to DecisionPending."
        state:isTriggeredBy D_ai_proposal
    ]

    relation instance T_decision_approved : state:Transition [
        from DecisionPending
        to Monitoring
        base:description "Operator approves AI action; return to Monitoring (or remain in DecisionPending until executed)."
        state:isTriggeredBy D_approval
    ]

    relation instance T_enter_historical : state:Transition [
        from Monitoring
        to HistoricalReview
        base:description "Operator requests historical view."
        state:isTriggeredBy D_history_request
    ]

    relation instance T_exit_historical : state:Transition [
        from HistoricalReview
        to Monitoring
        base:description "Operator returns from historical view to live monitoring."
        state:isTriggeredBy D_history_response
    ]

    relation instance T_shutdown : state:Transition [
        from Monitoring
        to DashboardFinal
        base:description "Operator or system shuts down dashboard session."
    ]

    ref instance Idle [ state:enables description1:A1 ]
    ref instance Monitoring [ state:enables description1:A2 ]
    ref instance Monitoring [ state:enables description1:A3 ]
    ref instance AlertHandling [ state:enables description1:A3 ]
    ref instance AlertHandling [ state:enables description1:A6 ]
    ref instance DecisionPending [ state:enables description1:A6 ]
    ref instance HistoricalReview [ state:enables description1:A2 ]


    instance AIFireWardenStateMachine : state:StateMachine [
        base:description "State machine modeling the AI Fire Warden behavior."
        state:isAllocatedTo description1:AIFireWarden
        base:contains WardenInitial
        base:contains Observing
        base:contains Advising
        base:contains AwaitingApproval
        base:contains ExecutingAction
        base:contains WardenFinal
    ]

    instance WardenInitial : state:Initial [
        base:description "AI Warden startup / idle."
    ]

    instance Observing : state:Mode [
        base:description "Warden monitors telemetry and looks for anomalies."
    ]

    instance Advising : state:Mode [
        base:description "Warden formulates a recommendation or task (advisory mode)."
    ]

    instance AwaitingApproval : state:Mode [
        base:description "Warden has proposed an action and is awaiting human approval."
    ]

    instance ExecutingAction : state:Mode [
        base:description "Warden is executing an approved mitigation task (autonomous actions)."
    ]

    instance WardenFinal : state:Final [
        base:description "Warden stops / safe state."
    ]

    relation instance W_T_idle_to_observing : state:Transition [
        from WardenInitial
        to Observing
        base:description "Warden becomes active and starts observation."
    ]

    relation instance W_T_detect_anomaly : state:Transition [
        from Observing
        to Advising
        base:description "While observing, Warden detects anomaly (smoke/fire)."
        state:isTriggeredBy D_alert
    ]

    relation instance W_T_propose_action : state:Transition [
        from Advising
        to AwaitingApproval
        base:description "Warden generates a structured proposal and sends to dashboard/operator."
        state:isTriggeredBy D_ai_proposal
    ]

    relation instance W_T_receive_approval : state:Transition [
        from AwaitingApproval
        to ExecutingAction
        base:description "Warden receives approval from operator; begins execution."
        state:isTriggeredBy D_approval
    ]

    relation instance W_T_execution_complete : state:Transition [
        from ExecutingAction
        to Observing
        base:description "Execution completes; return to Observing mode."

    ]

    ref instance Observing [ state:enables description1:A5 ]
    ref instance Advising [ state:enables description1:A5 ]
    ref instance AwaitingApproval [ state:enables description1:A6 ]
    ref instance ExecutingAction [ state:enables description1:A3 ]

    instance RespondToCriticalNotification : scenario:Scenario [
        base:description "Scenario showing flow when a critical notification is produced and acknowledged."
    ]

    instance RCN_Life_Dashboard : scenario:Lifeline [
        base:description "Dashboard lifeline"
        scenario:represents description1:DashboardSystem
    ]

    instance RCN_Life_AIWarden : scenario:Lifeline [
        base:description "AI Fire Warden lifeline"
        scenario:represents description1:AIFireWarden
    ]

    instance RCN_Life_Operator : scenario:Lifeline [
        base:description "Operator lifeline (human in control-room or field UI)"
        scenario:represents description1:Operator
    ]

    instance RCN_Life_FireCloud : scenario:Lifeline [
        base:description "Fire Cloud lifeline (source of telemetry/alerts)"
        scenario:represents description1:FireCloud
    ]

    instance RCN_TP1 : scenario:TimePoint []
    instance RCN_TP2 : scenario:TimePoint []
    instance RCN_TP3 : scenario:TimePoint []
    instance RCN_TP4 : scenario:TimePoint []
    instance RCN_TP5 : scenario:TimePoint []

    ref instance RCN_TP1 [ scenario:before RCN_TP2 ]
    ref instance RCN_TP2 [ scenario:before RCN_TP3 ]
    ref instance RCN_TP3 [ scenario:before RCN_TP4 ]
    ref instance RCN_TP4 [ scenario:before RCN_TP5 ]

    ref instance RCN_TP1 [ scenario:occursOn RCN_Life_FireCloud ]
    ref instance RCN_TP2 [ scenario:occursOn RCN_Life_Dashboard ]
    ref instance RCN_TP3 [ scenario:occursOn RCN_Life_Operator ]
    ref instance RCN_TP4 [ scenario:occursOn RCN_Life_Dashboard ]
    ref instance RCN_TP5 [ scenario:occursOn RCN_Life_AIWarden ]

    instance RCN_M1 : scenario:Message, scenario:Occurring [
        base:description "FireCloud sends critical alert to Dashboard"
        scenario:carries D_alert
    ]

    instance RCN_M2 : scenario:Message, scenario:Occurring  [
        base:description "Dashboard displays alert to Operator"
        scenario:carries D_alert
    ]

    instance RCN_M3 : scenario:Message, scenario:Occurring [
        base:description "Operator acknowledges alert (approval or ACK)"
        scenario:carries D_approval
    ]

    instance RCN_M4 : scenario:Message, scenario:Occurring [
        base:description "Dashboard forwards acknowledgment to AI Warden (if relevant) and FireCloud"
        scenario:carries D_approval
    ]

    instance RCN_M5 : scenario:Message, scenario:Occurring [
        base:description "AI Warden receives acknowledgment and may update internal state / telemetry"
        scenario:carries D_approval
    ]

    ref instance RCN_M1 [ scenario:atTime RCN_TP1 ]
    ref instance RCN_M2 [ scenario:atTime RCN_TP2 ]
    ref instance RCN_M3 [ scenario:atTime RCN_TP3 ]
    ref instance RCN_M4 [ scenario:atTime RCN_TP4 ]
    ref instance RCN_M5 [ scenario:atTime RCN_TP5 ]

    instance RCN_Exec_UpdateMap : scenario:Execution [
        base:description "Dashboard updates map visualization after receiving alert"
        scenario:executes description1:A3
    ]

    ref instance RCN_Exec_UpdateMap [ scenario:startsAt RCN_TP2 ]
    ref instance RCN_Exec_UpdateMap [ scenario:endsAt RCN_TP4 ]

    instance RCN_SF_DashboardMonitoring : scenario:StateFragment [
        base:description "Dashboard is in Monitoring when it receives the alert"
        scenario:checks Monitoring
        scenario:atTime RCN_TP2
    ]

    instance RCN_SF_WardenObserving : scenario:StateFragment [
        base:description "AI Warden is Observing at scenario start"
        scenario:checks Observing
        scenario:atTime RCN_TP1
    ]

    ref instance RespondToCriticalNotification [ scenario:hasLifeline RCN_Life_FireCloud ]
    ref instance RespondToCriticalNotification [ scenario:hasLifeline RCN_Life_Dashboard ]
    ref instance RespondToCriticalNotification [ scenario:hasLifeline RCN_Life_Operator ]
    ref instance RespondToCriticalNotification [ scenario:hasLifeline RCN_Life_AIWarden ]

    ref instance RespondToCriticalNotification [ scenario:hasFragment RCN_SF_DashboardMonitoring ]
    ref instance RespondToCriticalNotification [ scenario:hasFragment RCN_SF_WardenObserving ]


    instance ApproveAIWardenTask : scenario:Scenario [
        base:description "Scenario for the human-in-the-loop approval of an AI Warden task proposal."
    ]

    instance AAT_Life_AIWarden : scenario:Lifeline [
        base:description "AI Warden lifeline"
        scenario:represents description1:AIFireWarden
    ]

    instance AAT_Life_Dashboard : scenario:Lifeline [
        base:description "Dashboard lifeline"
        scenario:represents description1:DashboardSystem
    ]

    instance AAT_Life_Operator : scenario:Lifeline [
        base:description "Operator lifeline"
        scenario:represents description1:Operator
    ]

    instance AAT_TP1 : scenario:TimePoint []
    instance AAT_TP2 : scenario:TimePoint []
    instance AAT_TP3 : scenario:TimePoint []
    instance AAT_TP4 : scenario:TimePoint []
    ref instance AAT_TP1 [ scenario:before AAT_TP2 ]
    ref instance AAT_TP2 [ scenario:before AAT_TP3 ]
    ref instance AAT_TP3 [ scenario:before AAT_TP4 ]

    ref instance AAT_TP1 [ scenario:occursOn AAT_Life_AIWarden ]
    ref instance AAT_TP2 [ scenario:occursOn AAT_Life_Dashboard ]
    ref instance AAT_TP3 [ scenario:occursOn AAT_Life_Operator ]
    ref instance AAT_TP4 [ scenario:occursOn AAT_Life_AIWarden ]

    instance AAT_M1 : scenario:Message, scenario:Occurring [
        base:description "AI Warden sends structured proposal to Dashboard"
        scenario:carries D_ai_proposal
    ]

    instance AAT_M2 : scenario:Message, scenario:Occurring [
        base:description "Dashboard presents AI proposal to Operator (chat + approve UI)"
        scenario:carries D_ai_proposal
    ]

    instance AAT_M3 : scenario:Message, scenario:Occurring [
        base:description "Operator sends approval or rejection"
        scenario:carries D_approval
    ]

    instance AAT_M4 : scenario:Message, scenario:Occurring [
        base:description "Dashboard forwards approval to AI Warden"
        scenario:carries D_approval
    ]

    ref instance AAT_M1 [ scenario:atTime AAT_TP1 ]
    ref instance AAT_M2 [ scenario:atTime AAT_TP2 ]
    ref instance AAT_M3 [ scenario:atTime AAT_TP3 ]
    ref instance AAT_M4 [ scenario:atTime AAT_TP4 ]

    instance AAT_Exec_DisplayProposal : scenario:Execution [
        base:description "Dashboard displays AI proposal in chat interface (and shows Approve button)"
        scenario:executes description1:A4
    ]
    ref instance AAT_Exec_DisplayProposal [ scenario:startsAt AAT_TP2 ]
    ref instance AAT_Exec_DisplayProposal [ scenario:endsAt AAT_TP3 ]

    instance AAT_Exec_ProcessApproval : scenario:Execution [
        base:description "Dashboard processes approval and triggers D_approval to AI Warden"
        scenario:executes description1:A6
    ]
    ref instance AAT_Exec_ProcessApproval [ scenario:startsAt AAT_TP3 ]
    ref instance AAT_Exec_ProcessApproval [ scenario:endsAt AAT_TP4 ]

    instance AAT_SF_WardenAwaiting : scenario:StateFragment [
        base:description "Warden is AwaitingApproval while the proposal is pending."
        scenario:checks AwaitingApproval
        scenario:atTime AAT_TP1
    ]

    ref instance ApproveAIWardenTask [ scenario:hasLifeline AAT_Life_AIWarden ]
    ref instance ApproveAIWardenTask [ scenario:hasLifeline AAT_Life_Dashboard ]
    ref instance ApproveAIWardenTask [ scenario:hasLifeline AAT_Life_Operator ]
    ref instance ApproveAIWardenTask [ scenario:hasFragment AAT_SF_WardenAwaiting ]

    instance ViewHistoricalData : scenario:Scenario [
        base:description "Scenario demonstrating an Operator viewing historical fire states on the dashboard."
    ]

    instance VHD_Life_Operator : scenario:Lifeline [
        base:description "Operator lifeline"
        scenario:represents description1:Operator
    ]

    instance VHD_Life_Dashboard : scenario:Lifeline [
        base:description "Dashboard lifeline"
        scenario:represents description1:DashboardSystem
    ]

    instance VHD_Life_FireCloud : scenario:Lifeline [
        base:description "Fire Cloud lifeline (source of historical data)"
        scenario:represents description1:FireCloud
    ]

    instance VHD_TP1 : scenario:TimePoint []
    instance VHD_TP2 : scenario:TimePoint []
    instance VHD_TP3 : scenario:TimePoint []
    ref instance VHD_TP1 [ scenario:before VHD_TP2 ]
    ref instance VHD_TP2 [ scenario:before VHD_TP3 ]

    ref instance VHD_TP1 [ scenario:occursOn VHD_Life_Operator ]
    ref instance VHD_TP2 [ scenario:occursOn VHD_Life_Dashboard ]
    ref instance VHD_TP3 [ scenario:occursOn VHD_Life_FireCloud ]

    instance VHD_M1 : scenario:Message, scenario:Occurring [
        base:description "Operator requests historical data via dashboard UI"
        scenario:carries D_history_request
    ]

    instance VHD_M2 : scenario:Message, scenario:Occurring [
        base:description "Dashboard forwards history request to FireCloud"
        scenario:carries D_history_request
    ]

    instance VHD_M3 : scenario:Message, scenario:Occurring [
        base:description "FireCloud returns historical telemetry to Dashboard"
        scenario:carries D_history_response
    ]

    instance VHD_M4 : scenario:Message, scenario:Occurring [
        base:description "Dashboard renders historical data on the map/time-slider"
        scenario:carries D_history_response
    ]

    ref instance VHD_M1 [ scenario:atTime VHD_TP1 ]
    ref instance VHD_M2 [ scenario:atTime VHD_TP2 ]
    ref instance VHD_M3 [ scenario:atTime VHD_TP3 ]
    ref instance VHD_M4 [ scenario:atTime VHD_TP3 ]

    instance VHD_Exec_RequestHistory : scenario:Execution [
        base:description "NetworkInterface issues history fetch to FireCloud (A1)."
        scenario:executes description1:A1
    ]
    ref instance VHD_Exec_RequestHistory [ scenario:startsAt VHD_TP2 ]
    ref instance VHD_Exec_RequestHistory [ scenario:endsAt VHD_TP3 ]

    instance VHD_Exec_RenderHistory : scenario:Execution [
        base:description "Dashboard parses history and updates the map/historical layer (A2/A3)."
        scenario:executes description1:A2
    ]
    ref instance VHD_Exec_RenderHistory [ scenario:startsAt VHD_TP3 ]
    ref instance VHD_Exec_RenderHistory [ scenario:endsAt VHD_TP3 ]

    instance VHD_SF_DashboardHistorical : scenario:StateFragment [
        base:description "Dashboard is in HistoricalReview while rendering and user inspects past state."
        scenario:checks HistoricalReview
        scenario:atTime VHD_TP3
    ]

    ref instance ViewHistoricalData [ scenario:hasLifeline VHD_Life_Operator ]
    ref instance ViewHistoricalData [ scenario:hasLifeline VHD_Life_Dashboard ]
    ref instance ViewHistoricalData [ scenario:hasLifeline VHD_Life_FireCloud ]
    ref instance ViewHistoricalData [ scenario:hasFragment VHD_SF_DashboardHistorical ]

}
